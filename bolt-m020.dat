.del.*
!
!
! Mesh generation
! ===============
!  definition of the upper plate
!  -----------------------------
.point i 1 x  0
       i 2 x 10
       i 3 x 10 y 1
       i 4 x  0 y 1

.droite i 1 point 1 2
        i 2 point 2 3
        i 3 point 3 4
        i 4 point 4 1

.contour i 1 ligne 1 2 3 4
.domaine i 1 contour 1
.gen 
      modifie ligne 1 3 element 80
      modifie ligne 2 4 element 8
      numerote 1001 noeud
      numerote 1001 maille
      maille 1 transfini attribut 1


!
!  definition of the lower plate
!  -----------------------------
.point i 11 x  7 y -1
       i 12 x 17 y -1
       i 13 x 17
       i 14 x  7

.droite i 11 point 11 12
        i 12 point 12 13
        i 13 point 13 14
        i 14 point 14 11

.contour i 11 ligne 11 12 13 14
.domaine i 11 contour 11
.gen 
      modifie ligne 11 13 element 80
      modifie ligne 12 14 element 8
      maille 11 transfini attribut 11

!
!  definition of the bolt
!  ----------------------
.point i 21 x 7.5 y -1.5
       i 22 x 8   y -1.5
       i 23 x 9   y -1.5
       i 24 x 9.5 y -1.5
!
       i 25 x 7.5 y -1.
       i 26 x 8   y -1.
       i 27 x 9   y -1.
       i 28 x 9.5 y -1.
!
       i 31 x 7.5 y  1.
       i 32 x 8   y  1.
       i 33 x 9   y  1.
       i 34 x 9.5 y  1.
!
       i 35 x 7.5 y  1.5
       i 36 x 8   y  1.5
       i 37 x 9   y  1.5
       i 38 x 9.5 y  1.5

.droite i 21 point 21 22
        i 22 point 22 23
        i 23 point 23 24
        i 24 point 25 26
        i 25 point 26 27
        i 26 point 27 28
        i 27 point 21 25
        i 28 point 24 28

        i 31 point 31 32
        i 32 point 32 33
        i 33 point 33 34
        i 34 point 35 36
        i 35 point 36 37
        i 36 point 37 38
        i 37 point 31 35
        i 38 point 34 38

        i 41 point 26 32
        i 42 point 27 33


.contour i 21 ligne 21 22 23 28 26 25 24 27
         i 31 ligne 31 32 33 38 36 35 34 37
         i 41 ligne 25 42 32 41

.domaine i 21 contour 21
         i 31 contour 31
         i 41 contour 41

.gen
      modifie ligne 21 23 24 26 27 28 element 4
      modifie ligne 31 33 34 36 37 38 element 4
      modifie ligne 22 25             element 8
      modifie ligne 32 35             element 8
      modifie ligne 41 42             element 16
      maille 21 transfini c1 21 22 23 c2 28 c3 26 25 24 c4 27 attribut 21
      maille 31 transfini c1 31 32 33 c2 38 c3 36 35 34 c4 37 attribut 31
      maille 41 transfini                                     attribut 41


.hyp axisym
!
.mat i 1 yt 80000 nt 0.3

! Material
! ========

.ael att 1 11 21 31 41 mat 1

! Rib property
! ============
.sel group 11 maille 
                 attribut 1 11
     group 12 maille
                 boite structure xi 8 xs 9
     group 13 maille
                 identique 11 12
     group 21 maille
                 attribut 21 31 41

.ael group 13 21 nerv 16
.php group 13 can  val 1
     group 21 dvis val 1
!
! Group definition for boundary condition
! =======================================
!   contact
!   -------
.sel group 31 face ligne 41
     group 32 face ligne 42
     group 33 face ligne 11
     group 34 face ligne  3

     group 41 face peau 0
               boite structure xi 7.9 xs 8.1 yi -1 ys 1 orientation  1 0 0
     group 42 face peau 0  
               boite structure xi 8.9 xs 9.1 yi -1 ys 1 orientation -1 0 0
     group 43 face ligne 24
     group 44 face ligne 26
     group 45 face ligne 31
     group 46 face ligne 33

     group 51 face ligne 1
     group 52 face ligne 13
!
!   Bolt definition
!   ---------------
.sel group 81 face peau 0
             boite structu xi 7.9 xs 9.1 yi -0.1 ys 0.1 
     group 82 face peau 0
             maille att 41
     group 83 face
             identique 81 82
!
! Boundary condition
! ==================
.clm fix  ligne 4
     depl ligne 12 comp 1 val 0.1 nf 1 time

.fct cree fonc i 1
     cree valeur y u
     absci 0 1 2 ordo 0 0 1
!
! Bolt definition
! ===============
.noe i 1

.bolt i 81 ndist 1
           macro 1
           fix   2
           group 83 meth 1

.clm charge noeud i 1 comp 1 val 1 nf 2 time

.fct cree fonc i 2
     cree val y u
     absci 0 1 2 ordo 0 3000 3000

.sai archi mail i 1 comp 1 styp 9524 9530
!
! Contact definition
! ==================
.mct     i 11 group 41 gtar 31 opt 2 ksmo 1 nlim -1
.mct     i 21 group 42 gtar 32 opt 2 ksmo 1 nlim -1
.mct     i 31 group 43 gtar 33 opt 2 ksmo 1 nlim -1
.mct     i 41 group 44 gtar 33 opt 2 ksmo 1 nlim -1
.mct     i 51 group 45 gtar 34 opt 2 ksmo 1 nlim -1
.mct     i 61 group 46 gtar 34 opt 2 ksmo 1 nlim -1
.mct     i 71 group 51 gtar 52 opt 2 ksmo 1 nlim -1

!
! Strategy parameter
! ==================
.sub i 1
     meca 22
     cibl 1
     time 0 1
     dti0 0.25
     reso 5

     i 2
     hmax 0.1
     time 1 2


return
